plugins {
    id "java"
    id "idea"
    id "checkstyle"
    id "maven-publish"
    id "distribution"
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

repositories {
    mavenLocal()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
}

ext {
    r2dbcPostgresVersion = "0.9.3.RELEASE"
    flywayVersion = '10.8.1'
    postgresVersion = '42.4.0'
    springDocVersion = '1.6.14'

    testcontainersBomVersion = "1.17.3"
    reactorTestVersion = "3.6.5"
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    implementation "org.flywaydb:flyway-database-postgresql:${flywayVersion}"
    implementation "org.postgresql:postgresql:${postgresVersion}"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"

    implementation "org.springframework:spring-r2dbc"
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation "io.r2dbc:r2dbc-pool"
    implementation "org.postgresql:r2dbc-postgresql:${r2dbcPostgresVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation "org.springdoc:springdoc-openapi-webflux-ui:${springDocVersion}"
    implementation "io.micrometer:micrometer-registry-prometheus"

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    testImplementation(enforcedPlatform("org.testcontainers:testcontainers-bom:${testcontainersBomVersion}"))
    testImplementation "io.projectreactor:reactor-test:${reactorTestVersion}"
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testAnnotationProcessor "org.projectlombok:lombok"
    testCompileOnly "org.projectlombok:lombok"
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:postgresql"
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(Checkstyle) {
    config = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml')
    source = ['src/main/java', 'src/test/java']
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

checkstyle {
    toolVersion '10.4'
}

publishing {
    publications {
        myDistribution(MavenPublication) {
            artifact distTar
        }
    }
}

